plugins {
  id 'java'
  alias libs.plugins.node
}

node {
  version = libs.versions.nodeVersion
  pnpmVersion = libs.versions.pnpm
  download = true
}

tasks.register('pnpm_run_start', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall, spotlessCheck

  args = ['start']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'

  outputs.dir 'build'
}

tasks.register('pnpm_run_build', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall, spotlessCheck

  args = ['run', 'build']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'

  outputs.dir 'build'
}

tasks.register('pnpm_run_lint', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall

  args = ['run', 'lint']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'
  outputs.upToDateWhen { true }
}
tasks.register('pnpm_run_lint_fix', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall

  args = ['run', 'lint:fix']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'
  outputs.upToDateWhen { true }
}

tasks.register('pnpm_run_test', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall

  args = ['run', 'test']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'
  outputs.upToDateWhen { true }
}

tasks.register('buildPages', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall

  args = ['run', 'build_pages']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'

  doLast {
    copy {
      from "./dist/springwolf-ui/asyncapi-ui.html"
      into "dist/springwolf-ui"
      rename("asyncapi-ui.html", "index.html")
    }
  }
}

jar {
  dependsOn 'pnpm_run_build'
  metaInf { from 'dist/springwolf-ui' into 'resources/springwolf' }
}

java {
  withSourcesJar()
}

spotless {
  encoding 'UTF-8'

  format 'html', {
    target 'src/**/*.html'

    licenseHeader("<!-- SPDX-License-Identifier: Apache-2.0 -->", "<[^!].*>")

    trimTrailingWhitespace()
    endWithNewline()
  }

  format 'json', {
    target 'src/**/*.json'
    targetExclude '**/*.actual.json'

    trimTrailingWhitespace()
    endWithNewline()
  }

  format 'styling', {
    target 'src/**/*.ts', 'src/**/*.js', 'src/**/*.css' // , 'src/**/*.scss' currently broken

    licenseHeader("/* SPDX-License-Identifier: Apache-2.0 */", "import|export|.* \\{")

    trimTrailingWhitespace()
    endWithNewline()
  }
}

sourcesJar.dependsOn(pnpm_run_build)

tasks.named('assemble').configure {
  it.dependsOn('pnpm_run_build')
}
tasks.named('test').configure {
  it.dependsOn('pnpm_run_test', 'pnpm_run_lint_fix')
}
tasks.named('spotlessStyling').configure {
  it.dependsOn('nodeSetup', 'pnpmSetup', 'pnpm_run_lint')
}
tasks.named('spotlessHtml').configure {
  it.dependsOn('nodeSetup', 'pnpmSetup', 'pnpm_run_lint')
}
tasks.named('spotlessJson').configure {
  it.dependsOn('nodeSetup', 'pnpmSetup', 'pnpm_run_lint')
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'springwolf-ui'
        description = 'Web UI for Springwolf'
      }
    }
  }
}
