plugins {
  id 'java'
  id 'com.github.node-gradle.node' version '7.0.0'
}

node {
  version = '18.16.1'
  npmVersion = '9.5.1'
  download = true
}

npm_run_build {
  dependsOn spotlessCheck

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'package-lock.json'

  outputs.dir 'build'
}

task buildPages(type: NpmTask) {
  args = ['run', 'build_pages']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'package-lock.json'

  doLast {
    copy {
      from "./dist/springwolf-ui/asyncapi-ui.html"
      into "dist/springwolf-ui"
      rename("asyncapi-ui.html", "index.html")
    }
  }
}

jar {
  dependsOn 'npm_run_build'
  metaInf { from 'dist/springwolf-ui' into 'resources/springwolf' }
}

java {
  withSourcesJar()
}

spotless {
  encoding 'UTF-8'

  format 'styling', {
    target 'src/**/*.ts', 'src/**/*.js', 'src/**/*.json', 'src/**/*.html', 'src/**/*.css', 'src/**/*.scss'

    def npmExec = System.getProperty('os.name').toLowerCase().contains('windows') ? '/npm.cmd' : '/bin/npm'
    def nodeExec = System.getProperty('os.name').toLowerCase().contains('windows') ? '/node.exe' : '/bin/node'
    prettier()
      .npmExecutable("${tasks.named('npmSetup').get().npmDir.get()}${npmExec}") // get the npm executable path from gradle-node-plugin
      .nodeExecutable("${tasks.named('nodeSetup').get().nodeDir.get()}${nodeExec}")

    trimTrailingWhitespace()
    endWithNewline()
  }
}
tasks.named('spotlessStyling').configure {
  it.dependsOn('nodeSetup', 'npmSetup')
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'springwolf-ui'
        description = 'Web UI for Springwolf'
      }
    }
  }
}
