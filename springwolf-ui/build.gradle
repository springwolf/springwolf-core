plugins {
  id 'java'
  alias libs.plugins.node
}

node {
  version = libs.versions.nodeVersion
  npmVersion = libs.versions.npm
  download = true
}

tasks.register('npm_run_build', NpmTask) {
  dependsOn npmInstall, spotlessCheck

  args = ['run', 'build']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'package-lock.json'

  outputs.dir 'build'
}
assemble.dependsOn('npm_run_build')

tasks.register('npm_run_test', NpmTask) {
  dependsOn npmInstall

  args = ['run', 'test']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'package-lock.json'
  outputs.upToDateWhen { true }
}
test.dependsOn('npm_run_test')

tasks.register('buildPages', NpmTask) {
  args = ['run', 'build_pages']

  inputs.files fileTree("src")
  inputs.file 'angular.json'
  inputs.file 'package.json'
  inputs.file 'package-lock.json'

  doLast {
    copy {
      from "./dist/springwolf-ui/asyncapi-ui.html"
      into "dist/springwolf-ui"
      rename("asyncapi-ui.html", "index.html")
    }
  }
}

jar {
  dependsOn 'npm_run_build'
  metaInf { from 'dist/springwolf-ui' into 'resources/springwolf' }
}

java {
  withSourcesJar()
}

spotless {
  encoding 'UTF-8'

  def npmExec = System.getProperty('os.name').toLowerCase().contains('windows') ? '/npm.cmd' : '/bin/npm'
  def nodeExec = System.getProperty('os.name').toLowerCase().contains('windows') ? '/node.exe' : '/bin/node'

  format 'html', {
    target 'src/**/*.html'

    prettier()
      .npmExecutable("${tasks.named('npmSetup').get().npmDir.get()}${npmExec}")
      .nodeExecutable("${tasks.named('nodeSetup').get().nodeDir.get()}${nodeExec}")

    licenseHeader("<!-- SPDX-License-Identifier: Apache-2.0 -->", "<[^!].*>")

    trimTrailingWhitespace()
    endWithNewline()
  }

  format 'json', {
    target 'src/**/*.json'

    prettier()
      .npmExecutable("${tasks.named('npmSetup').get().npmDir.get()}${npmExec}")
      .nodeExecutable("${tasks.named('nodeSetup').get().nodeDir.get()}${nodeExec}")

    trimTrailingWhitespace()
    endWithNewline()
  }

  format 'styling', {
    target 'src/**/*.ts', 'src/**/*.js', 'src/**/*.css' // , 'src/**/*.scss' currently broken

    prettier()
      .npmExecutable("${tasks.named('npmSetup').get().npmDir.get()}${npmExec}")
      .nodeExecutable("${tasks.named('nodeSetup').get().nodeDir.get()}${nodeExec}")

    licenseHeader("/* SPDX-License-Identifier: Apache-2.0 */", "import|export|.* \\{")

    trimTrailingWhitespace()
    endWithNewline()
  }
}

sourcesJar.dependsOn(npm_run_build)

tasks.named('spotlessStyling').configure {
  it.dependsOn('nodeSetup', 'npmSetup')
}
tasks.named('spotlessHtml').configure {
  it.dependsOn('nodeSetup', 'npmSetup')
}
tasks.named('spotlessJson').configure {
  it.dependsOn('nodeSetup', 'npmSetup')
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'springwolf-ui'
        description = 'Web UI for Springwolf'
      }
    }
  }
}
