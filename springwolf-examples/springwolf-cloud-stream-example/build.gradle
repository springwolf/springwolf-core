plugins {
    id 'java'

    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'ca.cutterslade.analyze'

    id 'com.bmuschko.docker-spring-boot-application' version '8.1.0'
}

ext {
    set('springCloudVersion', "2022.0.1")
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    implementation project(":springwolf-core")

    runtimeOnly project(":springwolf-plugins:springwolf-cloud-stream-plugin")
    annotationProcessor project(":springwolf-plugins:springwolf-cloud-stream-plugin")

    runtimeOnly project(":springwolf-ui")
    runtimeOnly "org.springframework.boot:spring-boot-starter-web"

    implementation "com.asyncapi:asyncapi-core:${asyncapiCoreVersion}"
    implementation "org.apache.kafka:kafka-streams:${kafkaStreamsVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"
    implementation "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"

    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.boot:spring-boot"
    implementation "org.springframework:spring-beans"
    implementation "org.springframework:spring-context"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"

    testImplementation "com.vaadin.external.google:android-json:${androidJsonVersion}"
    testImplementation "org.assertj:assertj-core:${assertjCoreVersion}"
    testImplementation "org.springframework.boot:spring-boot-test"
    testImplementation "org.springframework:spring-test"

    testImplementation "org.springframework:spring-web"
    testImplementation "org.springframework.kafka:spring-kafka-test"
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "commons-io:commons-io:${commonsIoVersion}"
}

// We don't want to publish the examples
tasks.withType(PublishToMavenRepository).configureEach { it.enabled = false }
tasks.withType(PublishToMavenLocal).configureEach { it.enabled = false }

docker {
    springBootApplication {
        maintainer = 'shamir.stav@gmail.com'
        baseImage = 'eclipse-temurin:17-jre-focal'
        ports = [8080]
        images = ["stavshamir/springwolf-cloud-stream-example:${project.version}"]
    }

    registryCredentials {
        username = project.findProperty('DOCKERHUB_USERNAME') ?: ''
        password = project.findProperty('DOCKERHUB_TOKEN') ?: ''
    }
}

test {
    minHeapSize = "128m" // initial heap size
    maxHeapSize = "1024m" // maximum heap size

    dependsOn dockerBuildImage

    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}
