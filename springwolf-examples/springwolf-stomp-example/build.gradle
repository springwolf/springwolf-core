import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'

    alias libs.plugins.spring.boot
    alias libs.plugins.cutterslade.analyze

    alias libs.plugins.docker.spring.boot
}

dependencies {
    implementation project(":springwolf-core")
    implementation project(":springwolf-plugins:springwolf-stomp")
    permitUnusedDeclared project(":springwolf-plugins:springwolf-stomp")
    permitUsedUndeclared project(":springwolf-bindings:springwolf-stomp-binding")

    runtimeOnly project(":springwolf-ui")

    annotationProcessor libs.lombok
    testAnnotationProcessor libs.lombok
    compileOnly libs.lombok

    implementation libs.jakarta.servlet.api
    permitUnusedDeclared libs.jakarta.servlet.api
    implementation libs.jakarta.websocket.api
    permitUnusedDeclared libs.jakarta.websocket.api
    implementation libs.jakarta.websocket.client.api
    permitUnusedDeclared libs.jakarta.websocket.client.api

    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    runtimeOnly libs.spring.boot.starter.webmvc
    implementation libs.spring.boot
    implementation libs.spring.boot.autoconfigure
    implementation libs.spring.context
    implementation libs.spring.messaging
    implementation libs.spring.web
    implementation libs.spring.websocket

    implementation libs.swagger.annotations.jakarta

    implementation libs.commons.logging
    implementation libs.slf4j.api

    testImplementation libs.assertj.core

    testImplementation libs.spring.boot.test
    testImplementation libs.spring.boot.restclient
    permitTestUnusedDeclared libs.spring.boot.restclient
    testImplementation libs.spring.boot.restclient.test
    testImplementation libs.spring.beans
    testImplementation libs.spring.web

    testImplementation libs.testcontainers
    testImplementation libs.testcontainers.junit.jupiter

    testImplementation libs.jackson.core
    permitTestUnusedDeclared libs.jackson.core

    testImplementation libs.jspecify

    testAnnotationProcessor libs.lombok
    testCompileOnly libs.lombok

    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter
    testRuntimeOnly libs.junit.plattform.launcher
}

docker {
    springBootApplication {
        maintainer = 'shamir.stav@gmail.com'
        baseImage = 'eclipse-temurin:17-jre-focal'
        ports = [8080]
        images = ["stavshamir/springwolf-stomp-example:${project.version}"]
    }

    registryCredentials {
        username = project.findProperty('DOCKERHUB_USERNAME') as String ?: ''
        password = project.findProperty('DOCKERHUB_TOKEN') as String ?: ''
    }
}

test {
    dependsOn dockerBuildImage
    useJUnitPlatform()
}
