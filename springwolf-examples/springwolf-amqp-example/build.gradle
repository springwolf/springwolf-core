plugins {
    id 'java'

    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'

    id 'com.bmuschko.docker-spring-boot-application' version '8.1.0'
}

def isSnapshot = Boolean.valueOf(project.findProperty('SNAPSHOT'))

group 'io.github.springwolf'
version '0.7.0' + (isSnapshot ? '-SNAPSHOT' : '')

dependencies {
    implementation project(":springwolf-plugins:springwolf-amqp-plugin")
    compileOnly project(":springwolf-plugins:springwolf-amqp-plugin")
    annotationProcessor project(":springwolf-plugins:springwolf-amqp-plugin")
    runtimeOnly project(":springwolf-ui")

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.amqp:spring-rabbit"
    implementation "org.slf4j:slf4j-api"
    implementation "io.swagger.core.v3:swagger-core-jakarta:${swaggerCoreVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.junit.platform:junit-platform-launcher:${junitPlatformVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
    testImplementation "org.skyscreamer:jsonassert:${jsonAssertVersion}"
    testImplementation "commons-io:commons-io:${commonsIoVersion}"
}

docker {
    springBootApplication {
        maintainer = 'shamir.stav@gmail.com'
        baseImage = 'eclipse-temurin:17-jre-focal'
        ports = [8080]
        images = ["stavshamir/springwolf-amqp-example:${project.version}"]
    }

    registryCredentials {
        username = project.findProperty('DOCKERHUB_USERNAME') ?: ''
        password = project.findProperty('DOCKERHUB_TOKEN') ?: ''
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

test {
    dependsOn dockerBuildImage

    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}
