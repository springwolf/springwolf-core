plugins {
    id 'java'

    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'ca.cutterslade.analyze'

    id 'com.bmuschko.docker-spring-boot-application' version '8.1.0'
}

def isSnapshot = Boolean.valueOf(project.findProperty('SNAPSHOT'))

group 'io.github.springwolf'
version '0.7.0' + (isSnapshot ? '-SNAPSHOT' : '')

dependencies {
    implementation project(":springwolf-core")
    implementation project(":springwolf-plugins:springwolf-amqp-plugin")

    annotationProcessor project(":springwolf-plugins:springwolf-amqp-plugin")
    runtimeOnly project(":springwolf-ui")

    runtimeOnly "org.springframework.boot:spring-boot-starter-web"

    implementation "org.springframework.amqp:spring-rabbit"
    implementation "org.slf4j:slf4j-api"
    implementation "io.swagger.core.v3:swagger-annotations:${swaggerAnnotationsVersion}"
    implementation "com.asyncapi:asyncapi-core"

    implementation "org.springframework.amqp:spring-amqp"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.boot:spring-boot"
    implementation "org.springframework:spring-beans"
    implementation "org.springframework:spring-context"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter"

    testImplementation "commons-io:commons-io:${commonsIoVersion}"
    testImplementation "com.vaadin.external.google:android-json:${androidJsonVersion}"

    testImplementation "org.assertj:assertj-core"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.skyscreamer:jsonassert"

    testImplementation "org.springframework.boot:spring-boot-test"
    testImplementation "org.springframework:spring-web"
    testImplementation "org.springframework:spring-test"

    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"
}

docker {
    springBootApplication {
        maintainer = 'shamir.stav@gmail.com'
        baseImage = 'eclipse-temurin:17-jre-focal'
        ports = [8080]
        images = ["stavshamir/springwolf-amqp-example:${project.version}"]
    }

    registryCredentials {
        username = project.findProperty('DOCKERHUB_USERNAME') ?: ''
        password = project.findProperty('DOCKERHUB_TOKEN') ?: ''
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

test {
    dependsOn dockerBuildImage

    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}
