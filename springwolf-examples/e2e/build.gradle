plugins {
  id 'java'
  alias libs.plugins.node
}

node {
  version = libs.versions.nodeVersion
  npmVersion = libs.versions.npm
  download = true
}

npm_run_test {
  dependsOn spotlessCheck
  def springwolfExample = System.getenv("SPRINGWOLF_EXAMPLE") ?: "kafka"
  dependsOn(":springwolf-examples:springwolf-$springwolfExample-example:dockerBuildImage")

  inputs.files fileTree("tests")
  inputs.files fileTree("util")
  inputs.file 'playwright.config.ts'
  inputs.file 'package.json'
  inputs.file 'package-lock.json'
}

spotless {
  encoding 'UTF-8'

  def npmExec = System.getProperty('os.name').toLowerCase().contains('windows') ? '/npm.cmd' : '/bin/npm'
  def nodeExec = System.getProperty('os.name').toLowerCase().contains('windows') ? '/node.exe' : '/bin/node'

  format 'styling', {
    target 'tests/**/*.ts', 'tests/**/*.js', 'util/**/*.ts', 'util/**/*.js'

    prettier()
      .npmExecutable("${tasks.named('npmSetup').get().npmDir.get()}${npmExec}")
      .nodeExecutable("${tasks.named('nodeSetup').get().nodeDir.get()}${nodeExec}")

    licenseHeader("/* SPDX-License-Identifier: Apache-2.0 */", "import|export|.* \\{")

    trimTrailingWhitespace()
    endWithNewline()
  }
}

tasks.named('spotlessStyling').configure {
  it.dependsOn('nodeSetup', 'npmSetup')
}
