plugins {
  id 'java'
  alias libs.plugins.node
}

node {
  version = libs.versions.nodeVersion
  pnpmVersion = libs.versions.pnpm
  download = true
}

tasks.register('pnpm_run_install_deps', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall

  args = ['run', 'install:deps']

  inputs.file 'playwright.config.ts'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'
  outputs.upToDateWhen { true }
}
tasks.register('pnpmStart', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall

  args = ['start']

  inputs.file 'playwright.config.ts'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'
  outputs.upToDateWhen { true }
}

tasks.register('pnpm_run_lint', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall

  args = ['run', 'lint']

  inputs.files fileTree("tests")
  inputs.files fileTree("util")
  inputs.file 'playwright.config.ts'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'
  outputs.upToDateWhen { true }
}
tasks.register('pnpm_run_lint_fix', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall

  args = ['run', 'lint:fix']

  inputs.files fileTree("tests")
  inputs.files fileTree("util")
  inputs.file 'playwright.config.ts'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'
  outputs.upToDateWhen { true }
}

tasks.register('pnpm_run_test', PnpmTask) {
  group = "pnpm"
  dependsOn pnpmInstall, pnpm_run_install_deps
  def springwolfExample = System.getenv("SPRINGWOLF_EXAMPLE") ?: "kafka"
  dependsOn(":springwolf-examples:springwolf-$springwolfExample-example:dockerBuildImage")

  args = ['run', 'test']

  inputs.files fileTree("tests")
  inputs.files fileTree("util")
  inputs.file 'playwright.config.ts'
  inputs.file 'package.json'
  inputs.file 'pnpm-lock.yaml'
  outputs.upToDateWhen { true }
}

spotless {
  encoding 'UTF-8'

  format 'styling', {
    target 'tests/**/*.ts', 'tests/**/*.js', 'util/**/*.ts', 'util/**/*.js'

    licenseHeader("/* SPDX-License-Identifier: Apache-2.0 */", "import|export|.* \\{")

    trimTrailingWhitespace()
    endWithNewline()
  }
}

tasks.named('test').configure {
  it.dependsOn('pnpm_run_test', 'pnpm_run_lint_fix')
}
tasks.named('spotlessApply').configure {
  it.dependsOn('pnpm_run_lint_fix')
}
tasks.named('spotlessStyling').configure {
  it.dependsOn('nodeSetup', 'pnpmSetup', 'pnpm_run_lint')
}
