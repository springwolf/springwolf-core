plugins {
    id 'java'
    id 'org.springframework.boot' version '2.1.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'com.bmuschko.docker-spring-boot-application' version '6.1.4'
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
}

sourceCompatibility = '1.8'
version '0.7.0'


repositories {
    // For local development with snapshots
    mavenLocal() {
        content {
            includeGroup "io.github.springwolf"
        }
    }

    mavenCentral()
}

dependencies {
    implementation project(":springwolf-plugins:springwolf-kafka-plugin")
    implementation project(":springwolf-add-ons:springwolf-common-model-converters")
    runtimeOnly 'io.github.springwolf:springwolf-ui:0.4.0'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'com.google.guava:guava:27.0.1-jre'
    implementation 'org.javamoney:moneta:1.4.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    testImplementation 'org.jetbrains.kotlin:kotlin-reflect'
    testImplementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.10.2'
    testImplementation 'org.testcontainers:testcontainers:1.16.0'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
    testImplementation 'org.apache.directory.studio:org.apache.commons.io:2.4'
}

docker {
    springBootApplication {
        maintainer = 'shamir.stav@gmail.com'
        baseImage = 'openjdk:8-alpine'
        ports = [8080]
        images = ["stavshamir/springwolf-kafka-example:${project.version}"]
    }

    registryCredentials {
        username = project.findProperty('dockerhubUsername') ?: ''
        password = project.findProperty('dockerhubPassword') ?: ''
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    dependsOn dockerBuildImage

    testLogging {
        exceptionFormat = 'full'
    }
}