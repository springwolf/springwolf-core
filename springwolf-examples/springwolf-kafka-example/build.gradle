plugins {
    id 'java'

    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'ca.cutterslade.analyze'

    id 'com.bmuschko.docker-spring-boot-application'
    id 'org.springdoc.openapi-gradle-plugin' version '1.8.0'

    id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1'
    id 'com.google.protobuf' version '0.8.17'
}

dependencies {
    implementation project(":springwolf-core")
    implementation project(":springwolf-plugins:springwolf-kafka")

    runtimeOnly project(":springwolf-add-ons:springwolf-common-model-converters")
    runtimeOnly project(":springwolf-add-ons:springwolf-json-schema")
    runtimeOnly project(":springwolf-ui")

    annotationProcessor project(":springwolf-plugins:springwolf-kafka")

    runtimeOnly "org.springframework.boot:spring-boot-starter-web"

    implementation "org.springframework:spring-beans"
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-web"

    implementation "org.springframework.boot:spring-boot"
    implementation "org.springframework.boot:spring-boot-autoconfigure"

    implementation "org.springframework.kafka:spring-kafka"

    implementation "org.springframework.security:spring-security-config"
    implementation "org.springframework.security:spring-security-web"

    implementation "org.apache.avro:avro:${avroVersion}@jar"
    implementation "io.confluent:kafka-avro-serializer:${kafkaAvroSerializerVersion}"
    permitUnusedDeclared "io.confluent:kafka-avro-serializer:${kafkaAvroSerializerVersion}@jar"

    runtimeOnly "io.confluent:kafka-protobuf-serializer:${kafkaProtobufSerializerVersion}"
    implementation "com.google.protobuf:protobuf-java:${protobufJavaVersion}"
    implementation "com.hubspot.jackson:jackson-datatype-protobuf:${jacksonDatatypeProtobufVersion}"

    implementation "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

    implementation "io.swagger.core.v3:swagger-annotations:${swaggerVersion}"
    implementation "io.swagger.core.v3:swagger-core-jakarta:${swaggerVersion}"

    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"

    implementation "javax.money:money-api:${moneyApiVersion}"
    implementation "org.javamoney:moneta:${monetaVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"

    testImplementation "org.apache.kafka:kafka-clients:${kafkaClientsVersion}@jar"
    testImplementation "org.assertj:assertj-core:${assertjCoreVersion}"
    testImplementation "org.awaitility:awaitility:${awaitilityVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"

    testImplementation "org.springframework.boot:spring-boot-test"
    testImplementation "org.springframework.kafka:spring-kafka-test"
    testImplementation "org.springframework:spring-test"

    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersVersion}"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"

    permitTestUnusedDeclared "org.apache.kafka:kafka-clients:${kafkaClientsVersion}:test@jar"

    testImplementation("org.springframework.boot:spring-boot-starter-actuator")
    permitTestUnusedDeclared("org.springframework.boot:spring-boot-starter-actuator")

    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
}

docker {
    springBootApplication {
        maintainer = 'shamir.stav@gmail.com'
        baseImage = 'eclipse-temurin:17-jre-focal'
        ports = [8080]
        images = ["stavshamir/springwolf-kafka-example:${project.version}"]
    }

    registryCredentials {
        username = project.findProperty('DOCKERHUB_USERNAME') ?: ''
        password = project.findProperty('DOCKERHUB_TOKEN') ?: ''
    }
}

test {
    dependsOn dockerBuildImage
    // generate the open api docs before tests are executed so that if it works, the json is already there
    dependsOn generateOpenApiDocs
}

sourceSets {
    main {
        java {
            srcDirs = srcDirs + ['build/generated/source/proto/main/java']
        }
    }
    test {
        java {
            srcDirs = srcDirs + ['build/generated/source/proto/test/java']
        }
    }
}

openApi {
    apiDocsUrl = "http://localhost:8080/springwolf/docs"
    // For testing purposes we put the generated json into the test resources, but it could be any other directory
    outputDir = file("$buildDir/resources/test")
    outputFileName = "openapi-generated.json"
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.2'
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java { }
            }
        }
    }
}

afterEvaluate {
    tasks {
        forkedSpringBootRun {
            dependsOn bootJar
            dependsOn compileTestJava
            dependsOn dockerCreateDockerfile
            dependsOn dockerBuildImage
            dependsOn dockerSyncBuildContext
            dependsOn jar
            dependsOn processTestResources
            dependsOn spotlessJava
            doNotTrackState("See https://github.com/springdoc/springdoc-openapi-gradle-plugin/issues/102")
        }
    }
}
