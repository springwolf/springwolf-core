import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'

    alias libs.plugins.spring.boot
    alias libs.plugins.cutterslade.analyze

    alias libs.plugins.docker.spring.boot
    alias libs.plugins.openapi.gradle

    alias libs.plugins.avro
    alias libs.plugins.protobuf
}

dependencies {
    implementation project(":springwolf-core")
    implementation project(":springwolf-plugins:springwolf-kafka")
    permitUnusedDeclared project(":springwolf-plugins:springwolf-kafka")
    permitUsedUndeclared project(":springwolf-bindings:springwolf-kafka-binding")

    runtimeOnly project(":springwolf-add-ons:springwolf-common-model-converters")
    runtimeOnly project(":springwolf-add-ons:springwolf-json-schema")
    runtimeOnly project(":springwolf-ui")

    annotationProcessor project(":springwolf-plugins:springwolf-kafka")

    annotationProcessor libs.lombok
    compileOnly libs.lombok

    runtimeOnly platform(SpringBootPlugin.BOM_COORDINATES)
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation libs.spring.beans
    implementation libs.spring.boot
    implementation libs.spring.boot.autoconfigure
    implementation libs.spring.boot.jackson2
    implementation libs.spring.context
    implementation libs.spring.core
    implementation libs.spring.kafka
    implementation libs.spring.messaging
    implementation libs.spring.security.config
    implementation libs.spring.security.web
    implementation libs.spring.web
    runtimeOnly libs.spring.boot.starter.webmvc

    implementation libs.avro
    implementation libs.kafka.avro.serializer
    permitUnusedDeclared libs.kafka.avro.serializer

    runtimeOnly libs.kafka.protobuf.serializer
    implementation libs.protobuf.java
    implementation libs.jackson.datatype.protobuf
    implementation libs.jackson.annotations
    implementation libs.jackson.core
    implementation libs.jackson.databind

    implementation libs.jakarta.xml.bind.api

    implementation libs.swagger.annotations.outdated
    implementation libs.swagger.core.jakarta

    implementation libs.slf4j.api

    implementation libs.kafka.clients

    implementation libs.money.api
    implementation libs.moneta

    testImplementation libs.assertj.core
    testImplementation libs.awaitility

    testImplementation libs.spring.boot.restclient
    testImplementation libs.spring.boot.restclient.test
    testImplementation libs.spring.boot.test
    testImplementation libs.spring.core
    testImplementation libs.spring.test

    testImplementation libs.testcontainers
    testImplementation libs.testcontainers.junit.jupiter

    runtimeOnly libs.spring.boot.starter.actuator

    testAnnotationProcessor libs.lombok
    testCompileOnly libs.lombok

    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter
    testRuntimeOnly libs.junit.plattform.launcher

    testImplementation libs.commons.io
}

docker {
    springBootApplication {
        maintainer = 'shamir.stav@gmail.com'
        baseImage = 'eclipse-temurin:17-jre-focal'
        ports = [8080]
        images = ["stavshamir/springwolf-kafka-example:${project.version}"]
    }

    registryCredentials {
        username = project.findProperty('DOCKERHUB_USERNAME') as String ?: ''
        password = project.findProperty('DOCKERHUB_TOKEN') as String ?: ''
    }
}

test {
    dependsOn dockerBuildImage
    // generate the open api docs before tests are executed so that if it works, the json is already there
    dependsOn generateOpenApiDocs
    useJUnitPlatform()
}

openApi {
    apiDocsUrl = "http://localhost:8080/springwolf/docs"
    // For testing purposes we put the generated json into the test resources, but it could be any other directory
    outputDir = layout.buildDirectory.dir("resources/test")
    outputFileName = "openapi-generated.json"
}

protobuf {
    protoc {
        artifact = libs.protoc.get()
    }
}

afterEvaluate {
    tasks {
        forkedSpringBootRun {
            dependsOn bootJar
            dependsOn compileTestJava
            dependsOn dockerCreateDockerfile
            dependsOn dockerBuildImage
            dependsOn dockerSyncBuildContext
            dependsOn jar
            dependsOn processTestResources
            dependsOn spotlessJava
            dependsOn spotlessKotlin
            doNotTrackState("See https://github.com/springdoc/springdoc-openapi-gradle-plugin/issues/102")
        }
    }
}
