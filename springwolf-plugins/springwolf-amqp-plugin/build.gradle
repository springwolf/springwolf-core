plugins {
    id 'java-library'

    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'ca.cutterslade.analyze'
    id 'com.diffplug.spotless'
}

dependencies {
    api project(":springwolf-core")

    implementation "com.asyncapi:asyncapi-core:${asyncapiCoreVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"

    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-core"
    implementation "org.springframework:spring-web"
    implementation "org.springframework.boot:spring-boot"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.amqp:spring-amqp"
    implementation "org.springframework.amqp:spring-rabbit"

    compileOnly "com.google.code.findbugs:jsr305:${findbugsJSR305Version}"
    permitUnusedDeclared "com.google.code.findbugs:jsr305:${findbugsJSR305Version}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.assertj:assertj-core:${assertjCoreVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"

    testImplementation "org.springframework.boot:spring-boot-test"
    testImplementation "org.springframework:spring-beans"
    testImplementation "org.springframework:spring-test"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

jar {
    enabled = true
    archiveClassifier = ''
}
bootJar.enabled = false

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    dependsOn spotlessCheck

    useJUnitPlatform()
}

spotless {
    encoding 'UTF-8'

    java {

//        googleJavaFormat()
        googleJavaFormat().aosp()
//        eclipse()
//        clangFormat()
//        formatAnnotations()


        importOrder('', 'javax', 'java', '\\#')
        removeUnusedImports()

        trimTrailingWhitespace()
        endWithNewline()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'springwolf-amqp'
                description = 'Automated JSON API documentation for AMQP (RabbitMQ) Listeners built with Spring'
            }
        }
    }
}

