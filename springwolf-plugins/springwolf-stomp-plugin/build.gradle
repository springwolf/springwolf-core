import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'

    id 'org.springframework.boot'
    id 'ca.cutterslade.analyze'
}

dependencies {
    api project(":springwolf-asyncapi")
    api project(":springwolf-core")
    api project(":springwolf-bindings:springwolf-stomp-binding")
    permitUnusedDeclared project(":springwolf-bindings:springwolf-stomp-binding")

    runtimeOnly libs.swagger.models.jakarta

    implementation libs.jakarta.annotation.api
    implementation libs.slf4j.api

    annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)
    runtimeOnly platform(SpringBootPlugin.BOM_COORDINATES)
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation libs.spring.context
    implementation libs.spring.core
    implementation libs.spring.messaging

    implementation libs.spring.boot
    implementation libs.spring.boot.autoconfigure
    runtimeOnly libs.spring.boot.starter.websocket
    annotationProcessor libs.spring.boot.configuration.processor

    annotationProcessor libs.lombok
    compileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    testRuntimeOnly libs.spring.boot.starter.web

    testImplementation libs.mockito.core

    testImplementation libs.assertj.core

    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter
}

jar {
    enabled = true
    archiveClassifier = ''
}
bootJar.enabled = false

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'springwolf-stomp'
                description = 'Automated JSON API documentation for STOMP listeners (websocket) built with Spring'
            }
        }
    }
}
