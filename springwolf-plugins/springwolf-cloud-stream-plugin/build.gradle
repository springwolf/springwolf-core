import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'

    alias libs.plugins.spring.boot
    alias libs.plugins.cutterslade.analyze
}

dependencies {
    api project(":springwolf-asyncapi")
    api project(":springwolf-core")

    implementation libs.slf4j.api

    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation libs.spring.context
    implementation libs.spring.cloud.stream
    implementation libs.spring.boot.autoconfigure
    implementation libs.spring.core

    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testAnnotationProcessor libs.lombok

    testImplementation libs.assertj.core
    testImplementation libs.kafka.streams
    testImplementation libs.mockito.core

    testImplementation libs.spring.boot
    testImplementation libs.spring.boot.test
    testImplementation libs.spring.beans
    testImplementation libs.spring.test
    testImplementation libs.spring.messaging

    testImplementation libs.jackson.core
    permitTestUnusedDeclared libs.jackson.core

    testImplementation libs.junit.jupiter.api
    testRuntimeOnly libs.junit.jupiter
    testRuntimeOnly libs.junit.plattform.launcher
}

jar {
    enabled = true
    archiveClassifier = ''
}
bootJar.enabled = false

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'springwolf-cloud-stream'
                description = 'Automated JSON API documentation for Spring Boot cloud stream functions'
            }
        }
    }
}
