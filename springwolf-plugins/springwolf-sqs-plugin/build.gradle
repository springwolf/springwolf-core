plugins {
    id 'java-library'

    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'ca.cutterslade.analyze'
}

dependencyManagement {
    imports {
        mavenBom "io.awspring.cloud:spring-cloud-aws-dependencies:3.0.1"
    }
}

dependencies {
    api project(":springwolf-core")

    implementation "com.asyncapi:asyncapi-core:${asyncapiCoreVersion}"
    implementation "org.slf4j:slf4j-api:${slf4jApiVersion}"

    implementation 'io.awspring.cloud:spring-cloud-aws-sqs'
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-core"
    implementation "org.springframework:spring-web"
    implementation "org.springframework.boot:spring-boot"
    implementation "org.springframework.boot:spring-boot-autoconfigure"

    compileOnly "com.google.code.findbugs:jsr305:${jsr305Version}"
    permitUnusedDeclared "com.google.code.findbugs:jsr305:${jsr305Version}"

    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    testImplementation "org.assertj:assertj-core:${assertjCoreVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoCoreVersion}"

    testImplementation "org.springframework.boot:spring-boot-test"
    testImplementation "org.springframework:spring-beans"
    testImplementation "org.springframework:spring-test"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

jar {
    enabled = true
    archiveClassifier = ''
}
bootJar.enabled = false

java {
    withJavadocJar()
    withSourcesJar()
}

test {
    dependsOn spotlessApply // Automatically fix code formatting if possible

    useJUnitPlatform()
}

spotless {
    encoding 'UTF-8'

    java {
        palantirJavaFormat()

        importOrder('', 'javax', 'java', '\\#')
        replaceRegex("Remove wildcard imports", "import( static)?\\s+[^*\\s]+\\*;(\\r\\n|\\r|\\n)", "\$2")
        removeUnusedImports()

        trimTrailingWhitespace()
        endWithNewline()
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'springwolf-sqs'
                description = 'Automated JSON API documentation for AWS SQS Listeners built with Spring'
            }
        }
    }
}
