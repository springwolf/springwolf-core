plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'

    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

def isSnapshot = Boolean.valueOf(project.findProperty('SNAPSHOT'))

group 'io.github.springwolf'
version '0.1.2' + (isSnapshot ? '-SNAPSHOT' : '')

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.javamoney.moneta:moneta-core:${javaMoneyMonetaVersion}"
    implementation "io.swagger.core.v3:swagger-core:${swaggerCoreVersion}"

    implementation "org.springframework:spring-context"

    implementation "javax.xml.bind:jaxb-api:${jaxbApiVersion}"

    testImplementation "org.junit.platform:junit-platform-engine:${junitPlatformVersion}"
    testImplementation "org.junit.platform:junit-platform-commons:${junitPlatformVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
}

jar {
    enabled = true
    archiveClassifier = ''
}
bootJar.enabled = false

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId 'springwolf-common-model-converters'
            groupId project.group
            version project.version

            pom {
                name = 'springwolf-common-model-converters'
                description = 'Common model converters beans for Springwolf'
                url = 'https://springwolf.github.io/'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'stavshamir'
                        name = 'Stav Shamir'
                        email = 'shamir.stav@gmail.com'
                    }

                    developer {
                        id = 'timonback'
                        name = 'Timon Back'
                        email = 'timon.back+springwolf@gmail.com'
                    }

                    developer {
                        id = 'sam0r040'
                        name = 'David MÃ¼ller'
                        email = 'david.mueller@codecentric.de'
                    }
                }

                scm {
                    url = 'https://github.com/stavshamir/springwolf'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty('MAVEN_USERNAME') ?: ''
                password = project.findProperty('MAVEN_PASSWORD') ?: ''
            }
        }
    }
}

signing {
    def signingKey = project.findProperty("SIGNINGKEY")
    def signingPassword = project.findProperty("SIGNINGPASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
