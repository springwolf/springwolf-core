import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java-library'

    id 'org.springframework.boot'
    id 'ca.cutterslade.analyze'
}

dependencies {
    api project(":springwolf-asyncapi")
    testImplementation project(":springwolf-add-ons:springwolf-common-model-converters")
    permitTestUnusedDeclared project(":springwolf-add-ons:springwolf-common-model-converters")

    implementation libs.swagger.annotations.jakarta
    implementation libs.swagger.core.jakarta

    implementation libs.jackson.core
    implementation libs.jackson.databind

    implementation libs.slf4j.api

    annotationProcessor platform(SpringBootPlugin.BOM_COORDINATES)
    compileOnly platform(SpringBootPlugin.BOM_COORDINATES)
    implementation platform(SpringBootPlugin.BOM_COORDINATES)
    implementation libs.spring.boot

    implementation libs.spring.web
    implementation libs.spring.context
    implementation libs.spring.messaging
    implementation libs.spring.beans
    implementation libs.spring.core
    implementation libs.spring.boot.autoconfigure

    implementation libs.swagger.models.jakarta
    implementation libs.jakarta.annotation.api
    implementation libs.jakarta.validation.api

    implementation libs.commons.lang3

    compileOnly libs.spring.boot.actuator
    compileOnly libs.spring.webmvc
    compileOnly libs.lombok

    annotationProcessor libs.lombok
    annotationProcessor libs.spring.boot.configuration.processor

    testAnnotationProcessor libs.lombok
    testCompileOnly libs.lombok

    testImplementation libs.assertj.core
    testImplementation libs.awaitility
    testImplementation libs.mockito.core
    testImplementation libs.mockito.junit.jupiter
    testImplementation libs.spring.boot.test
    testImplementation libs.spring.test

    testImplementation libs.junit.jupiter.api
    testImplementation libs.junit.jupiter.params
    testRuntimeOnly libs.junit.jupiter
    testRuntimeOnly libs.slf4j.simple
}

jar {
    enabled = true
    archiveClassifier = ''
}
bootJar.enabled = false

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'springwolf-core'
                description = 'Automated JSON API documentation for async APIs (Kafka etc.) interfaces built with Spring'
            }
        }
    }
}
