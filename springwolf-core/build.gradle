plugins {
    id 'java'
    id 'java-library'
    id 'signing'
    id 'maven-publish'

    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
}

def isSnapshot = Boolean.valueOf(project.findProperty('SNAPSHOT'))

group = 'io.github.springwolf'
version = '0.10.0' + (isSnapshot ? '-SNAPSHOT' : '')

dependencies {
    api "com.asyncapi:asyncapi-core:${asyncApiCoreVersion}"

    implementation("io.swagger:swagger-inflector:${swaggerInflectorVersion}") {
        // swagger-inflector (2.0.9) transiently pulls two swagger-core dependencies:
        // - io.swagger.core.v3:swagger-core  2.x
        // - io.swagger:swagger-core  1.6.9
        // these deps should be excluded because we need 'io.swagger.core.v3:swagger-core-jakarkta 2.x'
        // Unfortunatly, io.swagger.swagger-core is still used in one place in swagger-inflector (io.swagger.util.Json)
        // so we cannot exclude the old 'io.swagger:swagger-core 1.6.9' dependeny. Instead, we exclude explicitly
        // the 'old' javax.validation groupid.

        exclude group: "io.swagger.core.v3"
//        exclude group: "io.swagger"  // not possible
        exclude group: "javax.validation"
    }

    implementation "io.swagger.core.v3:swagger-core-jakarta:${swaggerCoreVersion}"
    implementation "io.swagger.parser.v3:swagger-parser-core:${swaggerParserVersion}"

    implementation "org.springframework:spring-web"
    implementation "org.springframework:spring-context"
    implementation "org.springframework:spring-messaging"
    implementation "org.springframework.boot:spring-boot-autoconfigure"

    implementation "com.google.guava:guava:${guavaVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"


    testImplementation "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation "org.assertj:assertj-core:${assertjVersion}"

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.amqp:spring-rabbit"
    testImplementation "org.springframework.kafka:spring-kafka"
}

jar {
    enabled = true
    archiveClassifier = ''
}
bootJar.enabled = false

java {
    withJavadocJar()
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId 'springwolf-core'
            groupId project.group
            version project.version

            pom {
                name = 'springwolf-core'
                description = 'Automated JSON API documentation for async APIs (Kafka etc.) interfaces built with Spring'
                url = 'https://www.springwolf.dev'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'stavshamir'
                        name = 'Stav Shamir'
                        email = 'shamir.stav@gmail.com'
                    }

                    developer {
                        id = 'timonback'
                        name = 'Timon Back'
                        email = 'timon.back+springwolf@gmail.com'
                    }

                    developer {
                        id = 'sam0r040'
                        name = 'David MÃ¼ller'
                        email = 'david.mueller@codecentric.de'
                    }
                }

                scm {
                    url = 'https://github.com/springwolf/springwolf-core'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty('MAVEN_USERNAME') ?: ''
                password = project.findProperty('MAVEN_PASSWORD') ?: ''
            }
        }
    }
}

signing {
    def signingKey = project.findProperty("SIGNINGKEY")
    def signingPassword = project.findProperty("SIGNINGPASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
