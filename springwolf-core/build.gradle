plugins {
    id 'java'
    id 'java-library'
    id 'maven'
    id 'signing'
    id 'maven-publish'

    id 'org.springframework.boot' version '2.1.18.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

def isSnapshot = Boolean.valueOf(project.findProperty('SNAPSHOT'))

group = 'io.github.springwolf'
version = '0.8.0' + (isSnapshot ? '-SNAPSHOT' : '')
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api 'com.asyncapi:asyncapi-core:1.0.0-EAP'

    implementation 'io.swagger:swagger-inflector:2.0.8'

    implementation 'org.springframework:spring-web'
    implementation 'org.springframework:spring-context'
    implementation 'org.springframework:spring-messaging'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'

    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.10.8'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.2'

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'


    testImplementation 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'org.assertj:assertj-core:3.21.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit'
    testImplementation 'org.springframework.kafka:spring-kafka'
}

jar.enabled = true
bootJar.enabled = false

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
    useInMemoryPgpKeys project.findProperty("SIGNINGKEY"), project.findProperty("SIGNINGPASSWORD")
}

def pomConfig = {
    licenses {
        license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id 'stavshamir'
            name 'Stav Shamir'
            email 'shamir.stav@gmail.com'
        }
    }

    scm {
        url 'https://github.com/stavshamir/springwolf'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier "sources"
            }

            artifact javadocJar {
                classifier "javadoc"
            }

            groupId project.group
            artifactId 'springwolf-core'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Automated JSON API documentation for async APIs (Kafka etc.) interfaces built with Spring')
                root.appendNode('name', 'springwolf-core')
                root.appendNode('url', 'https://github.com/stavshamir/springwolf')
                root.children().last() + pomConfig

                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }

            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty('MAVEN_USERNAME') ?: ''
                password = project.findProperty('MAVEN_PASSWORD') ?: ''
            }
        }
    }

    model {
        tasks.publishMavenJavaPublicationToMavenLocal {
            dependsOn project.tasks.signArchives
        }
        tasks.publishMavenJavaPublicationToMavenRepository {
            dependsOn project.tasks.signArchives
        }
    }
}
