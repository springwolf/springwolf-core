plugins {
    id 'java'
    id 'java-library'
    id 'signing'
    id 'maven-publish'

    id 'org.springframework.boot' version '2.1.18.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

def isSnapshot = Boolean.valueOf(project.findProperty('SNAPSHOT'))

group = 'io.github.springwolf'
version = '0.8.0' + (isSnapshot ? '-SNAPSHOT' : '')
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api 'com.asyncapi:asyncapi-core:1.0.0-EAP'

    implementation 'io.swagger:swagger-inflector:2.0.8'

    implementation 'org.springframework:spring-web'
    implementation 'org.springframework:spring-context'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'

    implementation 'com.google.guava:guava:31.1-jre'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.10.8'
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.2'

    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'


    testImplementation 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'

    testImplementation 'org.assertj:assertj-core:3.21.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.amqp:spring-rabbit'
    testImplementation 'org.springframework.kafka:spring-kafka'
}

jar.enabled = true
bootJar.enabled = false

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifactId 'springwolf-core'
            groupId project.group
            version project.version

            pom {
                name = 'springwolf-core'
                description = 'Automated JSON API documentation for async APIs (Kafka etc.) interfaces built with Spring'
                url = 'https://springwolf.github.io'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'stavshamir'
                        name = 'Stav Shamir'
                        email = 'shamir.stav@gmail.com'
                    }

                    developer {
                        id = 'timonback'
                        name = 'Timon Back'
                        email = 'timon.back+springwolf@gmail.com'
                    }
                }

                scm {
                    url = 'https://github.com/stavshamir/springwolf'
                }
            }
        }
    }

    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = isSnapshot ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty('MAVEN_USERNAME') ?: ''
                password = project.findProperty('MAVEN_PASSWORD') ?: ''
            }
        }
    }
}

signing {
    def signingKey = project.findProperty("SIGNINGKEY")
    def signingPassword = project.findProperty("SIGNINGPASSWORD")
    useInMemoryPgpKeys (signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

